set(ENABLE_MODULES OFF)
if (ENABLE_MODULES)
    cmake_minimum_required(VERSION 3.28)
else()
    cmake_minimum_required(VERSION 3.18)
endif()

if (NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release)
endif()
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

project(co_async LANGUAGES CXX)

include(cmake/clang_std_modules.cmake)
# add_compile_options($<$<COMPILE_LANG_AND_ID:CXX,MSVC>:-utf-8>)

add_library(co_async OBJECT)
if (ENABLE_MODULES)
    enable_std_modules(co_async)
endif()
file(GLOB_RECURSE interface_sources co_async/*.cppm)
file(GLOB_RECURSE implementation_sources co_async/*.cpp)
if (ENABLE_MODULES)
    target_sources(co_async PUBLIC FILE_SET CXX_MODULES FILES ${interface_sources})
else()
    target_sources(co_async PUBLIC FILE_SET HEADERS FILES ${interface_sources})
endif()
target_sources(co_async PRIVATE ${implementation_sources})

if (CMAKE_SYSTEM_NAME MATCHES "Linux")
    target_sources(co_async PRIVATE arch/linux/liburing/liburing.cpp)
    target_include_directories(co_async PUBLIC arch/linux/liburing/include)
endif()

if (PROJECT_SOURCE_DIR STREQUAL CMAKE_SOURCE_DIR)
    file(GLOB example_sources examples/*.cpp)
    foreach(path ${example_sources})
        get_filename_component(name ${path} NAME_WE)
        add_executable(${name} ${path})
        if (ENABLE_MODULES)
            enable_std_modules(${name})
        endif()
        target_link_libraries(${name} PRIVATE co_async)
    endforeach()
endif()

